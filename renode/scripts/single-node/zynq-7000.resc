:name: Zynq-7000 SoC
:description: This script runs Linux on Zynq-7000 SoC.

using sysbus
$name?="Zynq"
mach create $name

machine LoadPlatformDescription @platforms/cpus/zynq-7000.repl
showAnalyzer uart0
sysbus Redirect 0xC0000000 0x0 0x10000000

$bin?=@https://dl.antmicro.com/projects/renode/zynq-interface-tests-vmlinux-s_14142952-ab5cd7445f31414fcbf8c79d49d737c669034ef2
$rootfs?=@https://dl.antmicro.com/projects/renode/zynq-interface-tests-rootfs.ext2-s_16777216-a08ef98709fb96de6662ed492eb45e95eb2127b3
$dtb?=@https://dl.antmicro.com/projects/renode/zynq-interface-tests.dtb-s_11724-984776b955e46b2d8f4426552a4c1ae91d063e4b

# Set timer frequency
ttc0 Frequency 33333333
ttc1 Frequency 33333333

macro reset
"""
# Set registers
cpu SetRegisterUnsafe 0 0x000
cpu SetRegisterUnsafe 1 0xD32 # processor variant (cortex-a9)
cpu SetRegisterUnsafe 2 0x100 # device tree address

sysbus LoadELF $bin
sysbus LoadFdt $dtb 0x100 "console=ttyPS0,115200 root=/dev/ram0 rw initrd=0x1a000000,16M" false
sysbus ZeroRange 0x1a000000 0x800000
sysbus LoadBinary $rootfs 0x1a000000
"""

runMacro $reset
