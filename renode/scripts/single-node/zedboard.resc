:name: Zedboard
:description: This script runs Linux on Zedboard.

using sysbus
$name?="Zedboard"
mach create $name

machine LoadPlatformDescription @platforms/boards/zedboard.repl

sysbus Redirect 0xC0000000 0x0 0x10000000

$bin?=@https://dl.antmicro.com/projects/renode/zynq--vmlinux-s_13611036-985e132f953222e5d0329f550905a70544301929
$rootfs?=@https://dl.antmicro.com/projects/renode/zynq--rootfs.ext2.gz-s_1874646-592c62e37a3fa5deea2577bafe086c4e96dc935a
$dtb?=@https://dl.antmicro.com/projects/renode/zynq-zed.dtb-s_11875-26effbd397b80938366bd18eddb1e89f56599c1d
### create externals ###

showAnalyzer sysbus.uart1

## set timer frequency ##
ttc0 Frequency 33333333
ttc1 Frequency 33333333

macro reset
"""
    ### set registers ###

    cpu SetRegisterUnsafe 0 0x000
    cpu SetRegisterUnsafe 1 0xD32 # board id
    cpu SetRegisterUnsafe 2 0x100 # device tree address
    sysbus.uart1 WriteDoubleWord 0x0 0x114 # force enable uart1 Tx and Rx

    ### load binaries ###

    sysbus LoadELF $bin
    sysbus LoadFdt $dtb 0x100 "console=ttyPS0,115200 root=/dev/ram0 rw earlyprintk initrd=0x1f645000,8M" false
    sysbus ZeroRange 0x1f645000 0x800000
    sysbus LoadBinary $rootfs 0x1f645000
"""

runMacro $reset
